
plugins {
    id "java"
    id "java-library"
    id "checkstyle"
    id "jacoco"
    id "signing"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "maven-publish"
    id "io.github.gradle-nexus.publish-plugin" version '1.2.0'
    id "idea"
}

java {
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenLocal()
    mavenCentral()
    // Before LaunchDarkly release artifacts get synced to Maven Central they are here along with snapshots:
    maven { url "https://oss.sonatype.org/content/groups/public/" }
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        showStandardStreams = true
        exceptionFormat = 'full'
    }
    dependsOn 'cleanTest'
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

checkstyle {
    toolVersion = "9.3"
    configFile file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    checkstyleTest.enabled = false
}

task generateJava(type: Copy) {
    // This updates Version.java
    from 'src/templates/java'
    into "src/main/java"
    filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [VERSION: version.toString()])
}

compileJava.dependsOn 'generateJava'

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifactId = 'launchdarkly-openfeature-serverprovider'

            pom {
                name = 'LaunchDarkly OpenFeature provider for the Server-Side SDK for Java'
                packaging = 'jar'
                description = 'Official LaunchDarkly OpenFeature provider for the Server-Side SDK for Java'
                url = 'https://github.com/launchdarkly/openfeature-java-server'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name = 'LaunchDarkly SDK Team'
                        email = 'sdks@launchdarkly.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/launchdarkly/openfeature-java-server'
                    developerConnection = 'scm:git:ssh:git@github.com:launchdarkly/openfeature-java-server.git'
                    url = 'https://github.com/launchdarkly/openfeature-java-server'
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

signing {
    required { findProperty("skipSigning") != "true" }
    sign publishing.publications.mavenJava
}

nexusPublishing {
    clientTimeout = java.time.Duration.ofMinutes(2) // we've seen extremely long delays in creating repositories
    repositories {
            sonatype()
    }
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:23.0'

    implementation group: 'com.launchdarkly', name: 'launchdarkly-java-server-sdk', version: '[7.1.0, 8.0.0)'

    implementation 'dev.openfeature:sdk:[1.7.0,2.0.0)'

    // Use JUnit test framework
    testImplementation(platform('org.junit:junit-bom:5.10.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation "org.mockito:mockito-core:3.+"
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}
